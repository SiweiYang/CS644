CompilationUnit:
  |PackageDeclaration| |ImportDeclarations| |TypeDeclaration| EOF

Literal:
  LITERAL_INT
  LITERAL_BOOL
  LITERAL_CHAR
  LITERAL_STRING
  LITERAL_NULL

Type:
  PrimitiveType
  ReferenceType

PrimitiveType:
  IntegralType
  KEYWORD_BOOLEAN

IntegralType:
  KEYWORD_BYTE
  KEYWORD_SHORT
  KEYWORD_INT
  KEYWORD_CHAR

ReferenceType:
  ClassOrInterfaceType
  ArrayType

ClassOrInterfaceType:
  Name

ClassType:
  ClassOrInterfaceType

InterfaceType:
  ClassOrInterfaceType

ArrayType:
  PrimitiveType [ ]
  Name [ ]

Name:
  SimpleName
  QualifiedName

SimpleName:
  IDENTIFIER

QualifiedName:
  Name . IDENTIFIER

ImportDeclarations:
  ImportDeclaration
  ImportDeclarations ImportDeclaration

PackageDeclaration:
  KEYWORD_PACKAGE Name ;

ImportDeclaration:
  SingleTypeImportDeclaration
  TypeImportOnDemandDeclaration

SingleTypeImportDeclaration:
  KEYWORD_IMPORT Name ;

TypeImportOnDemandDeclaration:
  KEYWORD_IMPORT Name . OPERATOR_* ;

TypeDeclaration:
  ClassDeclaration
  InterfaceDeclaration
  ;

Modifiers:
  ModifierKeyword
  Modifiers ModifierKeyword

ModifierKeyword:
  KEYWORD_ABSTRACT
  KEYWORD_FINAL
  KEYWORD_NATIVE
  KEYWORD_PRIVATE
  KEYWORD_PROTECTED
  KEYWORD_PUBLIC
  KEYWORD_STATIC

ClassDeclaration:
  |Modifiers| KEYWORD_CLASS IDENTIFIER |Super| |Interfaces| ClassBody

Super:
  KEYWORD_EXTENDS ClassType

Interfaces:
  KEYWORD_IMPLEMENTS InterfaceTypeList

InterfaceTypeList:
  InterfaceType
  InterfaceTypeList , InterfaceType

ClassBody:
  { |ClassBodyDeclarations| }

ClassBodyDeclarations:
  ClassBodyDeclaration
  ClassBodyDeclarations ClassBodyDeclaration

ClassBodyDeclaration:
  ClassMemberDeclaration
  StaticInitializer
  ConstructorDeclaration

ClassMemberDeclaration:
  FieldDeclaration
  MethodDeclaration

FieldDeclaration:
  |Modifiers| Type IDENTIFIER |OptionalAssignment| ;

OptionalAssignment:
  OPERATOR_= Expression

MethodDeclaration:
  MethodHeader MethodBody

MethodHeader:
  |Modifiers| Type MethodDeclarator |Throws|
  |Modifiers| KEYWORD_VOID MethodDeclarator |Throws|

MethodDeclarator:
  IDENTIFIER ( |FormalParameterList| )

FormalParameterList:
  FormalParameter
  FormalParameterList , FormalParameter

FormalParameter:
  Type IDENTIFIER

Throws:
  KEYWORD_THROWS ClassTypeList

ClassTypeList:
  ClassType
  ClassTypeList , ClassType

MethodBody:
  Block
  ;

StaticInitializer:
  KEYWORD_STATIC Block

ConstructorDeclaration:
  |Modifiers| ConstructorDeclarator |Throws| ConstructorBody

ConstructorDeclarator:
  SimpleName ( |FormalParameterList| )

ConstructorBody:
  { |ExplicitConstructorInvocation| |BlockStatements| }

ExplicitConstructorInvocation:
  KEYWORD_THIS ( |ArgumentList| ) ;

InterfaceDeclaration:
  |Modifiers| KEYWORD_INTERFACE IDENTIFIER |ExtendsInterfaces| InterfaceBody

ExtendsInterfaces:
  KEYWORD_EXTENDS InterfaceType
  ExtendsInterfaces , InterfaceType

InterfaceBody:
  { |InterfaceMemberDeclarations| }

InterfaceMemberDeclarations:
  InterfaceMemberDeclaration
  InterfaceMemberDeclarations InterfaceMemberDeclaration

InterfaceMemberDeclaration:
  MethodHeader ;

Block:
  { |BlockStatements| }

BlockStatements:
  BlockStatement
  BlockStatements BlockStatement

BlockStatement:
  LocalVariableDeclarationStatement
  Statement

LocalVariableDeclarationStatement:
  LocalVariableDeclaration ;

LocalVariableDeclaration:
  Type IDENTIFIER |OptionalAssignment|

Statement:
  StatementWithoutTrailingSubstatement
  IfThenStatement
  IfThenElseStatement
  WhileStatement
  ForStatement

StatementNoShortIf:
  StatementWithoutTrailingSubstatement
  IfThenElseStatementNoShortIf
  WhileStatementNoShortIf
  ForStatementNoShortIf

StatementWithoutTrailingSubstatement:
  Block
  EmptyStatement
  ExpressionStatement
  ReturnStatement

EmptyStatement:
  ;

ExpressionStatement:
  StatementExpression ;

StatementExpression:
  Assignment
  MethodInvocation
  ClassInstanceCreationExpression

IfThenStatement:
  KEYWORD_IF ( Expression ) Statement

IfThenElseStatement:
  KEYWORD_IF ( Expression ) StatementNoShortIf KEYWORD_ELSE Statement

IfThenElseStatementNoShortIf:
  KEYWORD_IF ( Expression ) StatementNoShortIf KEYWORD_ELSE StatementNoShortIf

WhileStatement:
  KEYWORD_WHILE ( Expression ) Statement

WhileStatementNoShortIf:
  KEYWORD_WHILE ( Expression ) StatementNoShortIf

ForStatement:
  KEYWORD_FOR ( |ForInit| ; |Expression| ; |StatementExpression| ) Statement

ForStatementNoShortIf:
  KEYWORD_FOR ( |ForInit| ; |Expression| ; |StatementExpression| ) StatementNoShortIf

ForInit:
  StatementExpression
  LocalVariableDeclaration

ReturnStatement:
  KEYWORD_RETURN |Expression| ;

Primary:
  PrimaryNoNewArray
  ArrayCreationExpression

PrimaryNoNewArray:
  Literal
  KEYWORD_THIS
  ( Expression )
  ClassInstanceCreationExpression
  FieldAccess
  MethodInvocation
  ArrayAccess

ClassInstanceCreationExpression:
  KEYWORD_NEW ClassType ( |ArgumentList| )

ArgumentList:
  Expression
  ArgumentList , Expression

ArrayCreationExpression:
  KEYWORD_NEW PrimitiveType DimExprs |Dims|
  KEYWORD_NEW ClassOrInterfaceType DimExprs |Dims|

DimExprs:
  DimExpr
  DimExprs DimExpr

DimExpr:
  [ Expression ]

Dims:
  [ ]
  Dims [ ]

FieldAccess:
  Primary . IDENTIFIER

MethodInvocation:
  Name ( |ArgumentList| )
  Primary . IDENTIFIER ( |ArgumentList| )

ArrayAccess:
  Name [ Expression ]
  PrimaryNoNewArray [ Expression ]

PostfixExpression:
  Primary
  Name

UnaryExpression:
  OPERATOR_- UnaryExpression
  UnaryExpressionNotPlusMinus

UnaryExpressionNotPlusMinus:
  PostfixExpression
  OPERATOR_! UnaryExpression
  CastExpression

CastExpression:
  ( PrimitiveType |Dims| ) UnaryExpression
  ( Expression ) UnaryExpressionNotPlusMinus
  ( Name Dims ) UnaryExpressionNotPlusMinus

MultiplicativeExpression:
  UnaryExpression
  MultiplicativeExpression MultiplicativeOperator UnaryExpression

MultiplicativeOperator:
  OPERATOR_*
  OPERATOR_/
  OPERATOR_%

AdditiveExpression:
  MultiplicativeExpression
  AdditiveExpression AdditiveOperator MultiplicativeExpression

AdditiveOperator:
  OPERATOR_+
  OPERATOR_-

RelationalExpression:
  AdditiveExpression
  RelationalExpression CompareOperator AdditiveExpression
  RelationalExpression KEYWORD_INSTANCEOF ReferenceType

CompareOperator:
  OPERATOR_<
  OPERATOR_>
  OPERATOR_<=
  OPERATOR_>=
  OPERATOR_!=

EqualityExpression:
  RelationalExpression
  EqualityExpression OPERATOR_== RelationalExpression

ConditionalAndExpression:
  EqualityExpression
  ConditionalAndExpression AndOperator EqualityExpression

AndOperator:
  OPERATOR_&
  OPERATOR_&&

ConditionalOrExpression:
  ConditionalAndExpression
  ConditionalOrExpression OrOperator ConditionalAndExpression

OrOperator:
  OPERATOR_|
  OPERATOR_||

ConditionalExpression:
  ConditionalOrExpression

Expression:
  ConditionalExpression
  Assignment

Assignment:
  LeftHandSide OPERATOR_= Expression

LeftHandSide:
  Name
  FieldAccess
  ArrayAccess
